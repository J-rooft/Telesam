apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 25
    buildToolsVersion "$rootProject.buildToolsVersion"

    defaultConfig {
        applicationId "space.naboo.telesam"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String[]", "apiKey", "{ ${generateKeyXorParts(properties.getProperty('apiKey')).collect {"\"${it}\""}.join(",") } }"
        buildConfigField "String[]", "apiHash", "{ ${generateKeyXorParts(properties.getProperty('apiHash')).collect {"\"${it}\""}.join(",") } }"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile "com.android.support:appcompat-v7:$rootProject.supportLibVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibVersion"
    compile "com.android.support:design:$rootProject.supportLibVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$rootProject.kotlin_version"

    compile 'io.reactivex.rxjava2:rxjava:2.1.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile "android.arch.lifecycle:runtime:$rootProject.archLifecycleVersion"
    compile "android.arch.lifecycle:extensions:$rootProject.archLifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$rootProject.archLifecycleVersion"

    compile "android.arch.persistence.room:runtime:$rootProject.archRoomVersion"
    compile "android.arch.persistence.room:rxjava2:$rootProject.archRoomVersion"
    kapt "android.arch.persistence.room:compiler:$rootProject.archRoomVersion"

    compile 'com.jakewharton.timber:timber:4.5.1'

    compile 'de.hdodenhof:circleimageview:2.1.0'

    // kotlogram deps
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'io.reactivex:rxjava:1.2.0'
}

repositories {
    mavenCentral()
}

static String[] generateKeyXorParts(String key) {
    String[] keyParts = new String[2]

    byte[] xorRandom = new byte[key.length()]
    byte[] xorMatch = new byte[key.length()]
    byte[] keyBytes = key.getBytes()
    for (int i = 0; i < key.length(); i++) {
        xorRandom[i] = (byte) (256 * Math.random())
        xorMatch[i] = (byte) (xorRandom[i] ^ keyBytes[i])
    }
    keyParts[0] = xorRandom.encodeBase64().toString()
    keyParts[1] = xorMatch.encodeBase64().toString()

    return keyParts
}
